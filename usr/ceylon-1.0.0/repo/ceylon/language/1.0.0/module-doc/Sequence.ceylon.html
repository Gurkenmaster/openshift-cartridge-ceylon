<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Sequence.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shAutoloader.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>&quot;A nonempty, immutable sequence of values. The type 
 `Sequence&lt;Element&gt;`, may be abbreviated `[Element+]`.
 
 Given a possibly-empty sequence of type `[Element*]`, 
 the `if (nonempty ...)` construct, or, alternatively,
 the `assert (nonempty ...)` construct, may be used to 
 narrow to a nonempty sequence type:
 
     [Integer*] nums = ... ;
     if (nonempty nums) {
         Integer first = nums.first;
         Integer max = max(nums);
         [Integer+] squares = nums.collect((Integer i) =&gt; i**2));
         [Integer+] sorted = nums.sort(byIncreasing((Integer i) =&gt; i));
     }
 
 Operations like `first`, `max()`, `collect()`, and 
 `sort()`, which polymorphically produce a nonempty or 
 non-null output when given a nonempty input are called 
 _emptiness-preserving_.&quot;
see (`interface Empty`)
by (&quot;Gavin&quot;)
shared interface Sequence&lt;out Element&gt;
        satisfies Element[] &amp; 
                  {Element+} &amp;
                  Cloneable&lt;[Element+]&gt; {
    
    &quot;The index of the last element of the sequence.&quot;
    see (`value Sequence.size`)
    shared actual formal Integer lastIndex;
    
    &quot;The first element of the sequence, that is, the
     element with index `0`.&quot;
    shared actual formal Element first;

    &quot;The last element of the sequence, that is, the
     element with index `sequence.lastIndex`.&quot;
    shared actual formal Element last;
    
    &quot;Returns `false`, since every `Sequence` contains at
     least one element.&quot;
    shared actual Boolean empty =&gt; false;
    
    &quot;Reverse this sequence, returning a new nonempty
     sequence.&quot;
    shared actual formal [Element+] reversed;
    
    &quot;This nonempty sequence.&quot;
    shared default actual [Element+] sequence =&gt; this;
    
    &quot;The rest of the sequence, without the first 
     element.&quot;
    shared actual formal Element[] rest;
    
    &quot;A nonempty sequence containing the elements of this
     container, sorted according to a function 
     imposing a partial order upon the elements.&quot;
    shared default actual [Element+] sort(
            &quot;The function comparing pairs of elements.&quot;
            Comparison comparing(Element x, Element y)) {
        value s = internalSort(comparing, this);
        //TODO: fix internalSort() and remove this assertion
        assert (nonempty s);
        return s;
    }

    &quot;A nonempty sequence containing the results of 
     applying the given mapping to the elements of this
     sequence.&quot;
    shared default actual [Result+] collect&lt;Result&gt;(
            &quot;The transformation applied to the elements.&quot;
            Result collecting(Element element)) {
        value s = map(collecting).sequence;
        assert (nonempty s);
        return s;
    }
    
    &quot;This nonempty sequence.&quot;
    shared actual default [Element+] clone =&gt; this;
    
    shared actual default String string 
            =&gt; (super of Sequential&lt;Element&gt;).string;
    
    shared actual default Boolean shorterThan(Integer length) 
            =&gt; (super of List&lt;Element&gt;).shorterThan(length);
    
    shared actual default Boolean longerThan(Integer length) 
            =&gt; (super of List&lt;Element&gt;).longerThan(length);
    
    shared default actual Element? findLast
                (Boolean selecting(Element elem))
            =&gt; (super of List&lt;Element&gt;).findLast(selecting);
    
    shared actual default Element[] repeat(Integer times)
            =&gt; (super of Element[]).repeat(times);
    
}
</pre></body></html>