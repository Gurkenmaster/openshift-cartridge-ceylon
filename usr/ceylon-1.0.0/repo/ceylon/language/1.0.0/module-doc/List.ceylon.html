<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>List.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shAutoloader.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>&quot;&quot;&quot;Represents a collection in which every element has a 
   unique non-negative integer index.
   
   A `List` is a `Collection` of its elements, and a 
   `Correspondence` from indices to elements.
   
   Direct access to a list element by index produces a
   value of optional type. The following idiom may be
   used instead of upfront bounds-checking, as long as 
   the list element type is a non-`null` type:
   
       value char = &quot;hello world&quot;[index];
       if (exists char) { /*do something*/ }
       else { /*out of bounds*/ }
   
   To iterate the indexes of a `List`, use the following
   idiom:
   
       for (i-&gt;char in &quot;hello world&quot;.indexed) { ... }&quot;&quot;&quot;
see (`interface Sequence`, 
     `interface Empty`, 
     `class Array`)
shared interface List&lt;out Element&gt;
        satisfies Collection&lt;Element&gt; &amp;
                  Correspondence&lt;Integer,Element&gt; &amp;
                  Ranged&lt;Integer,List&lt;Element&gt;&gt; &amp;
                  Cloneable&lt;List&lt;Element&gt;&gt; {
    
    &quot;The index of the last element of the list, or
     null if the list is empty.&quot;
    see (`value List.size`)
    shared formal Integer? lastIndex;
    
    &quot;The number of elements in this sequence, always
     `sequence.lastIndex+1`.&quot;
    see (`value List.lastIndex`)
    shared actual default Integer size =&gt; (lastIndex else -1) + 1;
    
    shared actual default Boolean shorterThan(Integer length) 
            =&gt; size&lt;length;
    
    shared actual default Boolean longerThan(Integer length) 
            =&gt; size&gt;length;
    
    &quot;The rest of the list, without the first element.&quot;
    shared actual formal List&lt;Element&gt; rest;
    
    &quot;Determines if the given index refers to an element
         of this sequence, that is, if
         `index&lt;=sequence.lastIndex`.&quot;
    shared actual default Boolean defines(Integer index) 
            =&gt; index &lt;= (lastIndex else -1);
	
    &quot;Returns the element of this sequence with the given
         index, or `null` if the given index is past the end
         of the sequence, that is, if
         `index&gt;sequence.lastIndex`. The first element of
         the sequence has index `0`.&quot;
    shared actual formal Element? get(Integer index);
    
    shared actual default Iterator&lt;Element&gt; iterator() {
        object listIterator
                satisfies Iterator&lt;Element&gt; {
            variable Integer index = 0;
            shared actual Element|Finished next() {
                if (index &lt;= (lastIndex else -1)) {
                    assert (is Element elem = outer.get(index++));
                    return elem;
                }
                else {
                    return finished;
                }
            }
        }
        return listIterator;
    }
    
    &quot;Reverse this list, returning a new list.&quot;
    shared formal List&lt;Element&gt; reversed;
    
    &quot;Two `List`s are considered equal iff they have the 
     same `size` and _entry sets_. The entry set of a 
     list `l` is the set of elements of `l.indexed`. 
     This definition is equivalent to the more intuitive 
     notion that two lists are equal iff they have the 
     same `size` and for every index either:
     
     - the lists both have the element `null`, or
     - the lists both have a non-null element, and the
       two elements are equal.&quot;
    shared actual default Boolean equals(Object that) {
        if (is List&lt;Anything&gt; that) {
            if (that.size==size) {
                for (i in 0..size-1) {
                    value x = this[i];
                    value y = that[i];
                    if (exists x) {
                        if (exists y) {
                            if (x!=y) {
                                return false;
                            }
                        }
                        else {
                            return false;
                        }
                    }
                    else if (exists y) {
                        return false;
                    }
                }
                else {
                    return true;
                }
            }
        }
        return false;
    }
    
    shared actual default Integer hash {
        variable value hash = 1;
        for (elem in this) {
            hash *= 31;
            if (exists elem) {
                hash += elem.hash;
            }
        }
        return hash;
    }
    
    shared default actual Element? findLast(
            Boolean selecting(Element elem)) {
        if (exists l=lastIndex) {
            variable value index = l;
            while (index &gt;= 0) {
                if (exists elem = this[index--]) {
                    if (selecting(elem)) {
                        return elem;
                    }
                }
            }
        }
        return null;
    }
    
    &quot;Returns the first element of this `List`, if any.&quot;
    shared actual default Element? first =&gt; this[0];
    
    &quot;Returns the last element of this `List`, if any.&quot;
    shared actual default Element? last {
        if (exists i = lastIndex) {
            return this[i];
        }
        else {
            return null;
        }
    }
    
    &quot;Returns a new `List` that starts with the specified
     element, followed by the elements of this `List`.&quot;
    see (`function following`)
    shared default [Element|Other+] withLeading&lt;Other&gt;(
            &quot;The first element of the resulting sequence.&quot;
            Other element) {
        value sb = SequenceBuilder&lt;Element|Other&gt;();
        sb.append(element);
        if (!empty) {
            sb.appendAll(this);
        }
        assert (nonempty seq=sb.sequence);
        return seq;
    }
    
    &quot;Returns a new `List` that contains the specified
     element appended to the end of the elements of this 
     `List`.&quot;
    shared default [Element|Other+] withTrailing&lt;Other&gt;(
            &quot;The last element of the resulting sequence.&quot;
            Other element) {
        value sb = SequenceBuilder&lt;Element|Other&gt;();
        if (!empty) {
            sb.appendAll(this);
        }
        sb.append(element);
        assert (nonempty seq=sb.sequence);
        return seq;
    }
    
    &quot;Determine if the given list occurs at the start of this 
     list.&quot;
    shared default Boolean startsWith(List&lt;Anything&gt; sublist)
            =&gt; includesAt(0, sublist);
    
    &quot;Determine if the given list occurs at the end of this
     list.&quot;
    shared default Boolean endsWith(List&lt;Anything&gt; sublist)
            =&gt; includesAt(size-sublist.size, sublist);
    
    &quot;Determine if the given list occurs at the given index 
     of this list.&quot;
    shared default Boolean includesAt(
            &quot;The index at which this list might occur&quot;
            Integer index, 
            List&lt;Anything&gt; sublist) {
        for (i in 0:sublist.size) {
            value x = this[index+i];
            value y = sublist[i];
            if (exists x) {
                if (exists y) {
                    if (x!=y) {
                        return false;
                    }
                }
                else {
                    return false;
                }
            }
            else if (exists y) {
                return false;
            }
        }
        else {
            return true;
        }
    }
    
    &quot;Determine if the given list occurs at some index in 
     this list.&quot;
    shared default Boolean includes(List&lt;Anything&gt; sublist) {
        for (index in 0:size) {
            if (includesAt(index,sublist)) {
                return true;
            }
        }
        return false;
    }
    
    &quot;The indexes in this list at which the given list 
     occurs.&quot;
    shared default {Integer*} inclusions(List&lt;Anything&gt; sublist) 
            =&gt; { for (index in 0:size) 
                    if (includesAt(index,sublist)) index };
    
    &quot;The first index in this list at which the given list 
     occurs.&quot;
    shared default Integer? firstInclusion(List&lt;Anything&gt; sublist) {
        for (index in 0:size) {
            if (includesAt(index,sublist)) {
                return index;
            }
        }
        else {
            return null;
        }
    }
    
    &quot;The last index in this list at which the given list 
     occurs.&quot;
    shared default Integer? lastInclusion(List&lt;Anything&gt; sublist) {
        for (index in (0:size).reversed) {
            if (includesAt(index,sublist)) {
                return index;
            }
        }
        else {
            return null;
        }
    }
    
    &quot;Determines if the given value occurs at the 
     given index in this list.&quot;
    shared default Boolean occursAt(Integer index, Anything element) {
         value elem = this[index];
         if (exists element) {
             if (exists elem) {
                 return elem==element;
             }
             else {
                 return false;
             }
         }
         else {
             return !elem exists;
         }
    }
    
    &quot;Determines if the given value occurs as an 
     element in this list.&quot;
    shared default Boolean occurs(Anything element) {
         for (index in 0:size) {
             if (occursAt(index,element)) {
                 return true;
             }
         }
         return false;
    }
    
    &quot;Determines if this list contains the given 
     value. Equivalent to `occurs()`.&quot;
    see (`function occurs`)
    shared actual default Boolean contains(Object element) 
            =&gt; occurs(element);
        
    &quot;The indexes in this list at which the given element 
     occurs.&quot;
    shared default {Integer*} occurrences(Anything element)
            =&gt; { for (index in 0:size) 
                    if (occursAt(index,element)) index };
    
    &quot;The first index in this list at which the given element 
     occurs.&quot;
    shared default Integer? firstOccurrence(Anything element) {
        for (index in 0:size) {
            if (occursAt(index,element)) {
                return index;
            }
        }
        else {
            return null;
        }
    }
    
    &quot;The last index in this list at which the given element 
     occurs.&quot;
    shared default Integer? lastOccurrence(Anything element) {
        for (index in (0:size).reversed) {
            if (occursAt(index,element)) {
                return index;
            }
        }
        else {
            return null;
        }
    }
        
    &quot;The indexes in this list for which the element 
     satisfies the given predicate.&quot;
    shared default {Integer*} indexes(
            &quot;The predicate the indexed elements must satisfy&quot;
            Boolean selecting(Element element)) 
            =&gt; { for (index in 0:size) 
                    //TODO: fix this awful hack
                    if (selecting(this[index] else nothing)) index };
    
    &quot;Trim the elements satisfying the given predicate
     function from the start and end of this list, 
     returning a list no longer than this list.&quot;
    shared default List&lt;Element&gt; trim(Boolean trimming(Element elem)) {
        if (exists l=lastIndex) {
            variable Integer from=-1;
            variable Integer to=-1;
            for (index in 0..l) {
                if (!trimming(this[index] else nothing)) {
                    from = index;
                    break;
                }
            }
            else {
                return [];
            }
            for (index in l..0) {
                if (!trimming(this[index] else nothing)) {
                    to = index;
                    break;
                }
            }
            else {
                return [];
            }
            return this[from..to];
        }
        else {
            return [];
        }
    }
    
    &quot;Trim the elements satisfying the given predicate
     function from the start of this list, returning 
     a list no longer than this list.&quot;
    shared default List&lt;Element&gt; trimLeading(Boolean trimming(Element elem)) {
        if (exists l=lastIndex) {
            for (index in 0..l) {
                if (!trimming(this[index] else nothing)) {
                    return this[index..l];
                }
            }
        }
        return [];
    }
    
    &quot;Trim the elements satisfying the given predicate
     function from the end of this list, returning a 
     list no longer than this list.&quot;
    shared default List&lt;Element&gt; trimTrailing(Boolean trimming(Element elem)) {
        if (exists l=lastIndex) {
            for (index in l..0) {
                if (!trimming(this[index] else nothing)) {
                    return this[0..index];
                }
            }
        }
        return [];
    }
    
    &quot;Select the first elements of this list, returning a 
     list no longer than the given length. If this list is 
     shorter than the given length, return this list. 
     Otherwise return a list of the given length.&quot;
    see (`function List.terminal`)
    shared default List&lt;Element&gt; initial(Integer length)
            =&gt; this[0:length];
    
    &quot;Select the last elements of the list, returning a list 
     no longer than the given length. If this list is 
     shorter than the given length, return this list. 
     Otherwise return a list of the given length.&quot;
    see (`function List.initial`)
    shared default List&lt;Element&gt; terminal(Integer length) {
        if (exists l = lastIndex, length&gt;0) {
            return this[l-length+1..l];
        }
        else {
            return [];
        }
    }
    
    /*&quot;Select the elements between the given indices. If 
         the start index is the same as the end index,
         return a list with a single element. If the start 
         index is larger than the end index, return the
         elements in the reverse order from the order in
         which they appear in this list. If both the
         start index and the end index are larger than the
         last index in the list, return an empty list. 
         Otherwise, if the last index is larger than the 
         last index in the list, return all elements from 
         the start index to last element of the list.&quot;
    shared actual formal List&lt;Element&gt; span(Integer from,
                                        Integer? to);
	
    &quot;Returns a list containing the elements beginning 
         from the given index, with the given length.&quot;
    shared actual formal List&lt;Element&gt; segment(Integer from,
                                           Integer length);*/
    
}
</pre></body></html>