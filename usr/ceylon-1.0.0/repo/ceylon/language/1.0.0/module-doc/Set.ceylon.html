<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Set.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shAutoloader.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>&quot;A collection of unique elements.
 
 A `Set` is a `Collection` of its elements.
 
 Sets may be the subject of the binary union, 
 intersection, and complement operators `|`, `&amp;`, and 
 `~`.&quot;
shared interface Set&lt;out Element&gt;
        satisfies Collection&lt;Element&gt; &amp;
                  Cloneable&lt;Set&lt;Element&gt;&gt;
        given Element satisfies Object {
    
    &quot;Determines if this `Set` is a superset of the 
     specified Set, that is, if this `Set` contains all 
     of the elements in the specified `Set`.&quot;
    shared default Boolean superset(Set&lt;Object&gt; set) {
        for (element in set) {
            if (!element in this) {
                return false;
            }
        }
        else {
            return true;
        }
    }
    
    &quot;Determines if this `Set` is a subset of the given 
     `Set`, that is, if the given set contains all of 
     the elements in this set.&quot;
    shared default Boolean subset(Set&lt;Object&gt; set) {
        for (element in this) {
            if (!element in set) {
                return false;
            }
        }
        else {
            return true;
        }
    }
    
    &quot;Two `Set`s are considered equal if they have the 
     same size and if every element of the first set is
     also an element of the second set, as determined
     by `contains()`.&quot;
    shared actual default Boolean equals(Object that) {
        if (is Set&lt;Object&gt; that,
                that.size==size) {
            for (element in this) {
                if (!element in that) {
                    return false;
                }
            }
            else {
                return true;
            }
        }
        return false;
    }
    
    shared actual default Integer hash {
        variable Integer hashCode = 1;
        for (Element elem in this){
            hashCode *= 31;
            hashCode += elem.hash;
        }
        return hashCode;
    }
    
    &quot;Returns a new `Set` containing all the elements of 
     this `Set` and all the elements of the given `Set`.&quot;
    shared formal Set&lt;Element|Other&gt; union&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object;
    
    &quot;Returns a new `Set` containing only the elements 
     that are present in both this `Set` and the given 
     `Set`.&quot;
    shared formal Set&lt;Element&amp;Other&gt; intersection&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object;
    
    &quot;Returns a new `Set` containing only the elements 
     contained in either this `Set` or the given `Set`, 
     but no element contained in both sets.&quot;
    shared formal Set&lt;Element|Other&gt; exclusiveUnion&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object;
    
    &quot;Returns a new `Set` containing all the elements in 
     this `Set` that are not contained in the given
     `Set`.&quot;
    shared formal Set&lt;Element&gt; complement&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object;
    
}
&quot;A [[Set]] with no elements.&quot;
shared object emptySet extends Object() satisfies Set&lt;Nothing&gt; {
    shared actual Set&lt;Other&gt; union&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object
        =&gt; set;
    shared actual Set&lt;Nothing&gt; intersection&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object
        =&gt; emptySet;
    shared actual Set&lt;Other&gt; exclusiveUnion&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object
        =&gt; set;
    shared actual Set&lt;Nothing&gt; complement&lt;Other&gt;(Set&lt;Other&gt; set)
            given Other satisfies Object
        =&gt; emptySet;
    shared actual Set&lt;Nothing&gt; clone =&gt; emptySet;
    shared actual Iterator&lt;Nothing&gt; iterator() =&gt; emptyIterator;
    shared actual Boolean subset(Set&lt;Object&gt; set) =&gt; true;
    shared actual Boolean superset(Set&lt;Object&gt; set) =&gt; set.empty;
    shared actual Integer size = 0;
    shared actual Boolean empty = true;
    shared actual Boolean contains(Object element) =&gt; false;
    shared actual Boolean containsAny({Object*} elements) =&gt; false;
    shared actual Boolean containsEvery({Object*} elements) =&gt; false;
    
    shared actual Integer count(
            Boolean selecting(Nothing element)) =&gt; 0;
    
    shared actual Set&lt;Nothing&gt; map&lt;Result&gt;(
            Result collecting(Nothing element)) =&gt; emptySet;
    
    shared actual Set&lt;Nothing&gt; filter
            (Boolean selecting(Nothing element)) =&gt; emptySet;
    
    shared actual Result fold&lt;Result&gt;(Result initial,
            Result accumulating(Result partial, Nothing element)) =&gt; 
            initial;
    
    shared actual Null find
            (Boolean selecting(Nothing element)) =&gt; null;
    
    shared actual [] collect&lt;Result&gt;
            (Result collecting(Nothing element)) =&gt; [];
    
    shared actual [] select
            (Boolean selecting(Nothing element)) =&gt; [];
    
    shared actual Boolean any
            (Boolean selecting(Nothing element)) =&gt; false;
    
    shared actual Boolean every
            (Boolean selecting(Nothing element)) =&gt; false;
    
    shared actual Set&lt;Nothing&gt; skipping(Integer skip) =&gt; emptySet;
    
    shared actual Set&lt;Nothing&gt; taking(Integer take) =&gt; emptySet;
    
    shared actual Set&lt;Nothing&gt; by(Integer step) =&gt; emptySet;
}
</pre></body></html>