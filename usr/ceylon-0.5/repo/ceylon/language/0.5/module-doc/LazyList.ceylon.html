<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>LazyList.ceylon</title><link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript' src='.resources/jquery-1.8.2.min.js'></script><script type='text/javascript' src='.resources/ceylondoc.js'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>doc &quot;An implementation of List that wraps an `Iterable` of
     elements. All operations on this List are performed on 
     the Iterable.&quot;
by &quot;Enrique Zamudio&quot;
shared class LazyList&lt;out Element&gt;({Element*} elems)
        satisfies List&lt;Element&gt; {
    
    shared actual Integer? lastIndex {
        value size = elems.size;
        return size &gt; 0 then size-1 else null;
    }
    
    shared actual Element? get(Integer index) {
        if (index == 0) {
            return elems.first;
        } 
        else {
            return elems.skipping(index).first;
        }
    }
    
    shared actual List&lt;Element&gt; rest =&gt; LazyList(elems.rest);
    
    shared actual Iterator&lt;Element&gt; iterator() =&gt; 
            elems.iterator();
    
    doc &quot;Returns a `List` with the elements of this `List` 
         in reverse order. This operation will create copy 
         the elements to a new `List`, so changes to the 
         original `Iterable` will no longer be reflected in 
         the new `List`.&quot;
    shared actual List&lt;Element&gt; reversed =&gt;
            elems.sequence.reversed;
    
    shared actual List&lt;Element&gt; clone =&gt; this;
    
    shared actual List&lt;Element&gt; span
            (Integer from, Integer to) {
        if (to &lt; 0 &amp;&amp; from &lt; 0) {
            return [];
        }
        Integer toIndex = largest(to,0);
        Integer fromIndex = largest(from,0);
        if (toIndex &gt;= fromIndex) {
            value els = fromIndex &gt; 0 
                    then elems.skipping(fromIndex)
                    else elems;
            return LazyList(els.taking(toIndex-fromIndex+1));
        } 
        else {
            //reversed
            value seq = toIndex&gt;0 
                    then elems.skipping(toIndex)
                    else elems;
            return seq.taking(fromIndex-toIndex+1)
                    .sequence.reversed;
        }
    }
    
    shared actual List&lt;Element&gt; spanTo(Integer to) =&gt;
            to&lt;0 then {} else LazyList(elems.taking(to+1));
    
    shared actual List&lt;Element&gt; spanFrom(Integer from) =&gt;
            from &gt; 0 then LazyList(elems.skipping(from))
                    else this;
    
    shared actual List&lt;Element&gt; segment
            (Integer from, Integer length) {
        if (length &gt; 0) {
            value els = from &gt; 0 then elems.skipping(from)
                    else elems;
            return LazyList(els.taking(length));
        } 
        else {
            return [];
        }
    }
    
    shared actual default Boolean equals(Object that) {
        if (is List&lt;Anything&gt; that) {
            value size = elems.size;
            if (that.size==size) {
                for (i in 0..size-1) {
                    value x = this[i];
                    value y = that[i];
                    if (exists x) {
                        if (exists y) {
                            if (x!=y) {
                                return false;
                            }
                        }
                        else {
                            return false;
                        }
                    }
                    else if (exists y) {
                        return false;
                    }
                }
                else {
                    return true;
                }
            }
        }
        return false;
    }
    
    shared actual default Integer hash {
        variable value hash = 1;
        for (elem in elems) {
            hash *= 31;
            if (exists elem) {
                hash += elem.hash;
            }
        }
        return hash;
    }
    
    shared default actual Element? findLast(
            Boolean selecting(Element elem)) =&gt;
                    elems.findLast(selecting);
    
    shared actual default Element? first =&gt; elems.first;
    
    shared actual default Element? last =&gt; elems.last;
    
}
</pre></body></html>