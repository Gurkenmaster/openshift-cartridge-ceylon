<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Map.ceylon</title><link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript' src='.resources/jquery-1.8.2.min.js'></script><script type='text/javascript' src='.resources/ceylondoc.js'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>doc &quot;Represents a collection which maps _keys_ to _items_,
     where a key can map to at most one item. Each such 
     mapping may be represented by an `Entry`.
     
     A `Map` is a `Collection` of its `Entry`s, and a 
     `Correspondence` from keys to items.
     
     The prescence of an entry in a map may be tested
     using the `in` operator:
     
         if (\&quot;lang\&quot;-&gt;\&quot;en_AU\&quot; in settings) { ... }
     
     The entries of the map may be iterated using `for`:
     
         for (key-&gt;item in settings) { ... }
     
     The item for a key may be obtained using the item
     operator:
     
         String lang = settings[\&quot;lang\&quot;] else \&quot;en_US\&quot;;&quot;
see (Entry, forKey, forItem, byItem, byKey)
shared interface Map&lt;out Key,out Item&gt;
        satisfies Collection&lt;Key-&gt;Item&gt; &amp;
                  Correspondence&lt;Object,Item&gt; &amp;
                  Cloneable&lt;Map&lt;Key,Item&gt;&gt;
        given Key satisfies Object
        given Item satisfies Object {
    
    doc &quot;Two `Map`s are considered equal iff they have the 
         same _entry sets_. The entry set of a `Map` is the
         set of `Entry`s belonging to the map. Therefore, the
         maps are equal iff they have same set of `keys`, and 
         for every key in the key set, the maps have equal
         items.&quot;
    shared actual default Boolean equals(Object that) {
        if (is Map&lt;Object,Object&gt; that,
                that.size==size) {
            for (entry in this) {
                if (exists item = that[entry.key],
                        item==entry.item) {
                    continue;
                }
                else {
                    return false;
                }
            }
            else {
                return true;
            }
        }
        else {
            return false;
        }
    }
    
    shared actual default Integer hash {
        variable Integer hashCode = 1;
        for (elem in this) {
            hashCode *= 31;
            hashCode += elem.hash;
        }
        return hashCode;
    }
    
    doc &quot;Returns the set of keys contained in this `Map`.&quot;
    actual shared default Set&lt;Key&gt; keys =&gt;
            LazySet ({ for (k-&gt;v in this) k });
    
    doc &quot;Returns all the values stored in this `Map`. An 
         element can be stored under more than one key in 
         the map, and so it can be contained more than once 
         in the resulting collection.&quot;
    shared default Collection&lt;Item&gt; values =&gt;
            LazyList ({ for (k-&gt;v in this) v });
    
    doc &quot;Returns a `Map` in which every key is an `Item` in 
         this map, and every value is the set of keys that 
         stored the `Item` in this map.&quot;
    shared default Map&lt;Item,Set&lt;Key&gt;&gt; inverse =&gt;
            LazyMap ({ for (key-&gt;item in this) item -&gt; 
                LazySet ({ for (k-&gt;i in this) if (i==item) k }) });
    
    doc &quot;Returns a `Map` with the same keys as this map. For
         every key, the item is the result of applying the
         given transformation function.&quot;
    shared default Map&lt;Key,Result&gt; mapItems&lt;Result&gt;(
            doc &quot;The function that transforms a key/item
                 pair, producing the item of the resulting
                 map.&quot;
            Result mapping(Key key, Item item)) 
            given Result satisfies Object =&gt;
                LazyMap ({ for (key-&gt;item in this) 
                            key-&gt;mapping(key,item) });
    
}
</pre></body></html>