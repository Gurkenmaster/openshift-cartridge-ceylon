<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Iterable.ceylon</title><link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript' src='.resources/jquery-1.8.2.min.js'></script><script type='text/javascript' src='.resources/ceylondoc.js'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>import ceylon.language { internalFirst = first }

doc &quot;Abstract supertype of containers whose elements may be 
     iterated. An iterable container need not be finite, but
     its elements must at least be countable. There may not
     be a well-defined iteration order, and so the order of
     iterated elements may not be stable.
     
     The type `Iterable&lt;Element,Null&gt;`, usually abbreviated
     `{Element*}` represents a possibly-empty iterable 
     container. The type `Iterable&lt;Element,Nothing&gt;`, 
     usually abbreviated `{Element+}` represents a nonempty 
     iterable container.
     
     An instance of `Iterable` may be constructed by 
     surrounding a value list in braces:
     
         {String+} words = { \&quot;hello\&quot;, \&quot;world\&quot; };
     
     An instance of `Iterable` may be iterated using a `for`
     loop:
     
         for (c in \&quot;hello world\&quot;) { ... }
     
     `Iterable` and its subtypes define various operations
     that return other iterable objects. Such operations 
     come in two flavors:
     
     - _Lazy_ operations return a \&quot;view\&quot; of the receiving
       iterable object. If the underlying iterable object is
       mutable, then changes to the underlying object will
       be reflected in the resulting view. Lazy operations
       are usually efficient, avoiding memory allocation or
       iteration of the receiving iterable object.
       
     - _Eager_ operations return an immutable object. If the
       receiving iterable object is mutable, changes to this
       object will not be reflected in the resulting 
       immutable object. Eager operations are often 
       expensive, involving memory allocation and iteration
       of the receiving iterable object.
     
     Lazy operations are preferred, because they can be 
     efficiently chained. For example:
     
         string.filter((Character c) =&gt; c.letter)
               .map((Character c) =&gt; c.uppercased)
     
     is much less expensive than:
     
         string.select((Character c) =&gt; c.letter)
               .collect((Character c) =&gt; c.uppercased)
     
     Furthermore, it is always easy to produce a new 
     immutable iterable object given the view produced by a
     lazy operation. For example:
     
         [ string.filter((Character c) =&gt; c.letter)
                 .map((Character c) =&gt; c.uppercased)... ]
     
     Lazy operations normally return an instance of 
     `Iterable` or `Map`.
     
     However, there are certain scenarios where an eager 
     operation is more useful, more convenient, or no more 
     expensive than a lazy operation, including:
     
     - sorting operations, which are eager by nature,
     - operations which preserve emptiness/nonemptiness of
       the receiving iterable object.
     
     Eager operations normally return a sequence.&quot;
see (Collection)
by &quot;Gavin&quot;
shared interface Iterable&lt;out Element, out Absent=Null&gt;
        satisfies Container&lt;Element,Absent&gt; 
        given Absent satisfies Null {
    
    doc &quot;An iterator for the elements belonging to this 
         container.&quot;
    shared formal Iterator&lt;Element&gt; iterator();
    
    doc &quot;Determines if the iterable object is empty, that is
         to say, if the iterator returns no elements.&quot;
    shared actual default Boolean empty =&gt;
            iterator().next() is Finished;
    
    shared default Integer size =&gt; count((Element e) =&gt; true);
    
    shared actual default Boolean contains(Object element) =&gt; 
            any(ifExists(element.equals));
    
    doc &quot;The first element returned by the iterator, if any.
         This should always produce the same value as
         `iterable.iterator().head`.&quot;
    shared actual default Absent|Element first =&gt;
            internalFirst(this);
    
    doc &quot;The last element returned by the iterator, if any.
         Iterables are potentially infinite, so calling this
         might never return; also, this implementation will
         iterate through all the elements, which might be
         very time-consuming.&quot;
    shared actual default Absent|Element last {
        variable Absent|Element e = first;
        for (x in this) {
            e = x;
        }
        return e;
    }
    
    doc &quot;Returns an iterable object containing all but the 
         first element of this container.&quot;
    shared default {Element*} rest =&gt; skipping(1);
    
    doc &quot;A sequence containing the elements returned by the
         iterator.&quot;
    shared default Element[] sequence =&gt; [ for (x in this) x ];
    
    doc &quot;An `Iterable` containing the results of applying
         the given mapping to the elements of to this 
         container.&quot;
    see (collect)
    shared default {Result*} map&lt;Result&gt;(
            doc &quot;The mapping to apply to the elements.&quot;
            Result collecting(Element elem)) =&gt;
                    { for (elem in this) collecting(elem) };
    
    /*shared default Callable&lt;{Result*},Args&gt; spread&lt;Result,Args&gt;(
            Callable&lt;Result,Args&gt; method(Element elem)) 
            given Args satisfies Anything[] =&gt;
                flatten((Args args) =&gt; 
                    { for (elem in this) unflatten(method(elem))(args) });*/
    
    doc &quot;An `Iterable` containing the elements of this 
         container that satisfy the given predicate.&quot;
    see (select)
    shared default {Element*} filter(
            doc &quot;The predicate the elements must satisfy.&quot;
            Boolean selecting(Element elem)) =&gt;
                    { for (elem in this) if (selecting(elem)) elem };
    
    doc &quot;The result of applying the accumulating function to 
         each element of this container in turn.&quot; 
    shared default Result fold&lt;Result&gt;(Result initial,
            doc &quot;The accumulating function that accepts an
                 intermediate result, and the next element.&quot;
            Result accumulating(Result partial, Element elem)) {
        variable value r = initial;
        for (e in this) {
            r = accumulating(r, e);
        }
        return r;
    }
    
    doc &quot;The first element which satisfies the given 
         predicate, if any, or `null` otherwise.&quot;
    shared default Element? find(
            doc &quot;The predicate the element must satisfy.&quot;
            Boolean selecting(Element elem)) {
        for (e in this) {
            if (selecting(e)) {
                return e;
            }
        }
        return null;
    }
    
    doc &quot;The last element which satisfies the given
         predicate, if any, or `null` otherwise.&quot;
    shared default Element? findLast(
            doc &quot;The predicate the element must satisfy.&quot;
            Boolean selecting(Element elem)) {
        variable Element? last = null;
        for (e in this) {
            if (selecting(e)) {
                last = e;
            }
        }
        return last;
    }
    
    doc &quot;A sequence containing the elements of this
         container, sorted according to a function 
         imposing a partial order upon the elements.
         
         For convenience, the functions `byIncreasing()` 
         and `byDecreasing()` produce a suitable 
         comparison function:
         
             \&quot;Hello World!\&quot;.sort(byIncreasing((Character c) =&gt; c.lowercased))
         
         This operation is eager by nature.&quot;
    see (byIncreasing, byDecreasing)
    shared default Element[] sort(
            doc &quot;The function comparing pairs of elements.&quot;
            Comparison comparing(Element x, Element y)) =&gt;
                    internalSort(comparing, this);
    
    doc &quot;A sequence containing the results of applying the
         given mapping to the elements of this container. An 
         eager counterpart to `map()`.&quot;
    see (map)
    shared default Result[] collect&lt;Result&gt;(
            doc &quot;The transformation applied to the elements.&quot;
            Result collecting(Element element)) =&gt;
                    map(collecting).sequence;
    
    doc &quot;A sequence containing the elements of this 
         container that satisfy the given predicate. An 
         eager counterpart to `filter()`.&quot;
    see (filter)
    shared default Element[] select(
            doc &quot;The predicate the elements must satisfy.&quot;
            Boolean selecting(Element element)) =&gt;
                    filter(selecting).sequence;
    
    doc &quot;Return `true` if at least one element satisfies the
         predicate function.&quot;
    shared default Boolean any(
            doc &quot;The predicate that at least one element 
                 must satisfy.&quot;
            Boolean selecting(Element e)) {
        for (e in this) {
            if (selecting(e)) {
                return true;
            }
        }
        return false;
    }
    
    doc &quot;Return `true` if all elements satisfy the predicate
         function.&quot;
    shared default Boolean every(
            doc &quot;The predicate that all elements must 
                 satisfy.&quot;
            Boolean selecting(Element e)) {
        for (e in this) {
            if (!selecting(e)) {
                return false;
            }
        }
        return true;
    }
    
    doc &quot;Produce an `Iterable` containing the elements of
         this iterable object, after skipping the first 
         `skip` elements. If this iterable object does not 
         contain more elements than the specified number of 
         elements, the `Iterable` contains no elements.&quot;
    shared default {Element*} skipping(Integer skip) {
        if (skip &lt;= 0) { 
            return this;
        }
        else {
            object iterable satisfies {Element*} {
                shared actual Iterator&lt;Element&gt; iterator() {
                    value iter = outer.iterator();
                    variable value i=0;
                    while (i++&lt;skip &amp;&amp; !iter.next() is Finished) {}
                    return iter;
                }
                shared actual Element? first {
                    if (!is Finished first = iterator().next()) {
                        return first;
                    }
                    else {
                        return null;
                    }
                }
                shared actual Element? last =&gt; outer.last;
            }
            return iterable;
        }
    }
    
    doc &quot;Produce an `Iterable` containing the first `take`
         elements of this iterable object. If the specified 
         number of elements is larger than the number of 
         elements of this iterable object, the `Iterable` 
         contains the same elements as this iterable object.&quot;
    shared default {Element*} taking(Integer take) {
        if (take &lt;= 0) { 
            return {}; 
        }
        else {
            object iterable satisfies {Element*} {
                shared actual Iterator&lt;Element&gt; iterator() {
                    value iter = outer.iterator();
                    object iterator satisfies Iterator&lt;Element&gt; {
                        variable value i=0;
                        actual shared Element|Finished next() {
                            return ++i&gt;take then finished 
                                    else iter.next();
                        }
                    }
                    return iterator;
                }
                shared actual Element? first =&gt; outer.first;
                shared actual Element? last =&gt; outer.last; //TODO!!!!
            }
            return iterable;
        }
    }
    
    doc &quot;Produce an `Iterable` containing every `step`th 
         element of this iterable object. If the step size 
         is `1`, the `Iterable` contains the same elements 
         as this iterable object. The step size must be 
         greater than zero. The expression
         
             (0..10).by(3)
         
         results in an iterable object with the elements
         `0`, `3`, `6`, and `9` in that order.&quot;
    throws (Exception, &quot;if the given step size is nonpositive, 
                        i.e. `step&lt;1`&quot;) //TODO: this is an assertion
    shared default {Element*} by(Integer step) {
        doc &quot;step size must be greater than zero&quot;
        assert (step &gt; 0);
        if (step == 1) {
            return this;
        } 
        else {
            object iterable satisfies Iterable&lt;Element,Absent&gt; {
                shared actual Iterator&lt;Element&gt; iterator() {
                    value iter = outer.iterator();
                    object iterator satisfies Iterator&lt;Element&gt; {
                        shared actual Element|Finished next() {
                            value next = iter.next();
                            variable value i=0;
                            while (++i&lt;step &amp;&amp; 
                                    !iter.next() is Finished) {}
                            return next;
                        }
                    }
                    return iterator;
                }
            }
            return iterable;
        }
    }
    
    doc &quot;Return the number of elements in this `Iterable` 
         that satisfy the predicate function.&quot;
    shared default Integer count(
            doc &quot;The predicate satisfied by the elements to
                 be counted.&quot;
            Boolean selecting(Element element)) {
        variable value count=0;
        for (elem in this) {
            if (exists elem) {
                if (selecting(elem)) {
                    count++;
                }
            }
        }
        return count;
    }
    
    doc &quot;The non-null elements of this `Iterable`, in their
         original order. For null elements of the original 
         `Iterable`, there is no entry in the resulting 
         iterable object.&quot;
    shared default {Element&amp;Object*} coalesced =&gt;
            { for (e in this) if (exists e) e };
    
    doc &quot;All entries of form `index-&gt;element` where `index` 
         is the position at which `element` occurs, for every
         non-null element of this `Iterable`, ordered by
         increasing `index`. For a null element at a given
         position in the original `Iterable`, there is no 
         entry with the corresponding index in the resulting 
         iterable object. The expression 
         
             { \&quot;hello\&quot;, null, \&quot;world\&quot; }.indexed
             
         results in an iterable object with the entries
         `0-&gt;\&quot;hello\&quot;` and `2-&gt;\&quot;world\&quot;`.&quot;
    shared default {&lt;Integer-&gt;Element&amp;Object&gt;*} indexed {
        object indexes
                satisfies {&lt;Integer-&gt;Element&amp;Object&gt;*} {
                value orig = outer;
            shared actual Iterator&lt;Integer-&gt;Element&amp;Object&gt; iterator() {
                object iterator satisfies Iterator&lt;Integer-&gt;Element&amp;Object&gt; {
                    value iter = orig.iterator();
                    variable value i=0;
                    shared actual &lt;Integer-&gt;Element&amp;Object&gt;|Finished next() {
                        variable value next = iter.next();
                        while (!next exists) {
                            i++;
                            next = iter.next();
                        }
                        if (!is Finished n=next, exists n) {
                            return i++-&gt;n;
                        } else {
                            return finished;
                        }
                    }
                }
                return iterator;
            }
        }
        return indexes;
    }
    
    doc &quot;An `Iterable` with the given inital element followed 
         by the elements of this iterable object.&quot;
    shared default {Element|Other+} following&lt;Other&gt;(Other head) {
        //TODO: should be {leading,*outer} when that is efficient
        object cons satisfies {Element|Other+} {
            shared actual Iterator&lt;Element|Other&gt; iterator() {
                value iter = outer.iterator();
                object iterator satisfies Iterator&lt;Element|Other&gt; {
                    variable Boolean first = true;
                    shared actual Element|Other|Finished next() {
                        if (first) {
                            first=false;
                            return head;
                        }
                        else {
                            return iter.next();
                        }
                    }
                }
                return iterator;
            }
        }
        return cons;
    }
    
    doc &quot;The elements of this iterable object, in their
         original order, followed by the elements of the 
         given iterable object also in their original 
         order.&quot;
    shared default {Element|Other*} chain&lt;Other&gt;({Other*} other) {
        object chained satisfies {Element|Other*} {
            shared actual Iterator&lt;Element|Other&gt; iterator() =&gt;
                    ChainedIterator(outer, other);
        }
        return chained;
    }
    
    doc &quot;An `Iterable` that produces the elements of this 
         iterable object, replacing every `null` element 
         with the given default value. The resulting iterable
         object does not produce the value `null`.&quot;
    shared default Iterable&lt;Element&amp;Object|Default&gt;
    defaultNullElements&lt;Default&gt;(
            doc &quot;A default value that replaces `null` elements.&quot;
            Default defaultValue)
        =&gt; { for (elem in this) elem else defaultValue };
    
    /*doc &quot;Creates a Map that contains this `Iterable`&apos;s
         elements, grouped in `Sequence`s under the
         keys provided by the grouping function.&quot;
    shared default native Map&lt;Grouping,[Element+]&gt; group&lt;Grouping&gt;(
                doc &quot;A function that must return the key under
                     which to group the specified element.&quot;
                Grouping grouping(Element elem))
            given Grouping satisfies Object;*/
        
    doc &quot;A string of form `\&quot;{ x, y, z }\&quot;` where `x`, `y`, 
         and `z` are the `string` representations of the 
         elements of this collection, as produced by the
         iterator of the collection, or the string `\&quot;{}\&quot;` 
         if this iterable is empty. If the number of items
         is very large only a certain amount of them might
         be shown followed by \&quot;...\&quot;.&quot;
    shared actual default String string {
        if (empty) {
            return &quot;{}&quot;;
        }
        else {
            String list = commaList(taking(30));
            return &quot;{ `` sizeExceeds(this, 30) 
                        then list + &quot;, ...&quot; 
                        else list `` }&quot;; 
        }
    }
    
}

//TODO: make this a method of Iterable
Boolean sizeExceeds(Iterable&lt;Anything&gt; iterable, 
        Integer size) {
    variable Integer count=0;
    for (x in iterable) {
        if (size&lt;count++) {
            return true;
        }
    }
    else {
        return false;
    }
}

String commaList({Anything*} elements) =&gt;
        &quot;, &quot;.join { for (element in elements)
                    element?.string else &quot;null&quot; };

Boolean ifExists(Boolean predicate(Object val))(Anything val) {
    if (exists val) {
        return predicate(val);
    }
    else {
        return false;
    }
}
</pre></body></html>