<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>List.ceylon</title><link href='.resources/shCore.css' rel='stylesheet' type='text/css'/>
<link href='.resources/shThemeDefault.css' rel='stylesheet' type='text/css'/>
<script type='text/javascript' src='.resources/jquery-1.8.2.min.js'></script><script type='text/javascript' src='.resources/ceylondoc.js'></script><script src='.resources/shCore.js' type='text/javascript'></script><script src='.resources/shBrushCeylon.js' type='text/javascript'></script></head><body><pre class='brush: ceylon'>doc &quot;Represents a collection in which every element has a 
     unique non-negative integer index.
     
     A `List` is a `Collection` of its elements, and a 
     `Correspondence` from indices to elements.
     
     Direct access to a list element by index produces a
     value of optional type. The following idiom may be
     used instead of upfront bounds-checking, as long as 
     the list element type is a non-`null` type:
     
         value char = \&quot;hello world\&quot;[index];
         if (exists char) { /*do something*/ }
         else { /*out of bounds*/ }
     
     To iterate the indexes of a `List`, use the following
     idiom:
     
         for (i-&gt;char in \&quot;hello world\&quot;.indexed) { ... }
     
     &quot;
see (Sequence, Empty, Array)
shared interface List&lt;out Element&gt;
        satisfies Collection&lt;Element&gt; &amp;
                  Correspondence&lt;Integer,Element&gt; &amp;
                  Ranged&lt;Integer,List&lt;Element&gt;&gt; &amp;
                  Cloneable&lt;List&lt;Element&gt;&gt; {
    
    doc &quot;The index of the last element of the list, or
         null if the list is empty.&quot;
    see (size)
    shared formal Integer? lastIndex;
    
    doc &quot;The number of elements in this sequence, always
         `sequence.lastIndex+1`.&quot;
    see (lastIndex)
    shared actual default Integer size =&gt; (lastIndex else -1) + 1;
    
    doc &quot;The rest of the list, without the first element.&quot;
    shared actual formal List&lt;Element&gt; rest;
    
    doc &quot;Determines if the given index refers to an element
         of this sequence, that is, if
         `index&lt;=sequence.lastIndex`.&quot;
    shared actual default Boolean defines(Integer index) =&gt; 
            index &lt;= (lastIndex else -1);
	
    doc &quot;Returns the element of this sequence with the given
         index, or `null` if the given index is past the end
         of the sequence, that is, if
         `index&gt;sequence.lastIndex`. The first element of
         the sequence has index `0`.&quot;
    shared actual formal Element? get(Integer index);
    
    shared actual default Iterator&lt;Element&gt; iterator() {
        object listIterator
                satisfies Iterator&lt;Element&gt; {
            variable Integer index = 0;
            shared actual Element|Finished next() {
                if (index &lt;= (lastIndex else -1)) {
                    assert (is Element elem = outer.get(index++));
                    return elem;
                }
                else {
                    return finished;
                }
            }
        }
        return listIterator;
    }
    
    doc &quot;Reverse this list, returning a new list.&quot;
    shared formal List&lt;Element&gt; reversed;
    
    doc &quot;Two `List`s are considered equal iff they have the 
         same `size` and _entry sets_. The entry set of a 
         list `l` is the set of elements of `l.indexed`. 
         This definition is equivalent to the more intuitive 
         notion that two lists are equal iff they have the 
         same `size` and for every index either:
         
         - the lists both have the element `null`, or
         - the lists both have a non-null element, and the
           two elements are equal.&quot;
    shared actual default Boolean equals(Object that) {
        if (is List&lt;Anything&gt; that) {
            if (that.size==size) {
                for (i in 0..size-1) {
                    value x = this[i];
                    value y = that[i];
                    if (exists x) {
                        if (exists y) {
                            if (x!=y) {
                                return false;
                            }
                        }
                        else {
                            return false;
                        }
                    }
                    else if (exists y) {
                        return false;
                    }
                }
                else {
                    return true;
                }
            }
        }
        return false;
    }
    
    shared actual default Integer hash {
        variable value hash = 1;
        for (elem in this) {
            hash *= 31;
            if (exists elem) {
                hash += elem.hash;
            }
        }
        return hash;
    }
    
    //disabled because of compiler bug resolving
    //ambiguity in Sequence
    /*shared default actual Element? findLast(
            Boolean selecting(Element elem)) {
        if (exists l=lastIndex) {
            variable value index = l;
            while (index &gt;= 0) {
                if (exists elem = this[index--]) {
                    if (selecting(elem)) {
                        return elem;
                    }
                }
            }
        }
        return null;
    }*/
    
    doc &quot;Returns the first element of this `List`, if any.&quot;
    shared actual default Element? first =&gt; this[0];
    
    doc &quot;Returns the last element of this `List`, if any.&quot;
    shared actual default Element? last {
        if (exists i = lastIndex) {
            return this[i];
        }
        return null;
    }
    
    doc &quot;Returns a new `List` that starts with the specified
         element, followed by the elements of this `List`.&quot;
    see (following)
    shared default [Element|Other+] withLeading&lt;Other&gt;(
            doc &quot;The first element of the resulting sequence.&quot;
            Other element) {
        value sb = SequenceBuilder&lt;Element|Other&gt;();
        sb.append(element);
        if (!empty) {
            sb.appendAll(this);
        }
        assert (nonempty seq=sb.sequence);
        return seq;
    }
    
    doc &quot;Returns a new `List` that contains the specified
         element appended to the end of the elements of this 
         `List`.&quot;
    shared default [Element|Other+] withTrailing&lt;Other&gt;(
            doc &quot;The last element of the resulting sequence.&quot;
            Other element) {
        value sb = SequenceBuilder&lt;Element|Other&gt;();
        if (!empty) {
            sb.appendAll(this);
        }
        sb.append(element);
        assert (nonempty seq=sb.sequence);
        return seq;
    }
    
    /*doc &quot;Select the elements between the given indices. If 
         the start index is the same as the end index,
         return a list with a single element. If the start 
         index is larger than the end index, return the
         elements in the reverse order from the order in
         which they appear in this list. If both the
         start index and the end index are larger than the
         last index in the list, return an empty list. 
         Otherwise, if the last index is larger than the 
         last index in the list, return all elements from 
         the start index to last element of the list.&quot;
    shared actual formal List&lt;Element&gt; span(Integer from,
                                        Integer? to);
	
    doc &quot;Returns a list containing the elements beginning 
         from the given index, with the given length.&quot;
    shared actual formal List&lt;Element&gt; segment(Integer from,
                                           Integer length);*/
    
}
</pre></body></html>